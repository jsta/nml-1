s        = ( stmt | <comment> | <junk> )*

array    = name sect
c        = real | string | logical | complex
colon    = ':'
comma    = ','
comment  = <wsopt> '!' #'[^\n]*' <wsopt> | <wsopt> '\n'
complex  = <'('> <wsopt> real <wsopt> <','> <wsopt> real <wsopt> <')'>
dataref  = partref ( percent partref )*
dec      = dot uint?
dot      = '.'
exp      = sign uint | 'e' <wsopt> int | 'd' <wsopt> int
false    = 'f' | 'F'
int      = sign? uint
junk     = #'[^&]+'
logical  = <'.'?> ( true | false ) <#'[^/,\s]*'>
name     = #'[a-zA-Z]' #'[a-zA-Z0-9_]{0,30}'
nvseq    = nvsubseq ( ( <sep> | <ws> ) nvsubseq )* <sep>?
nvsubseq = <comment>* dataref <wsopt> <'='> <wsopt> values <comment>*
partref  = array | name
percent  = '%'
r        = uint
real     = int dec? exp? | sign? dec int exp?
sect     = <'('> ( int? colon int? | int ) ( colon int )? <')'>
sep      = <wsopt> comma <wsopt>
sign     = '-' | '+'
slash    = '/'
star     = '*'
stmt     = <wsopt> <'&'> name <ws> nvseq? <wsopt> <slash> <wsopt>
string   = "'" ( #'[^\']' | "''" )* "'" | '"' ( #'[^\"]' | '""' )* '"'
true     = 't' | 'T'
uint     = #'[0-9]+'
value    = r star c | r star | c
values   = value ( ( <sep> | <ws> ) value )* <sep>? | <comment>
ws       = #'\s+'
wsopt    = #'\s*'
